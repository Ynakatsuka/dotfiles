#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...

#
# Prompt
#
export LSCOLORS=gxfxcxdxbxegedabagacad

# Google Cloud config info for prompt
setopt PROMPT_SUBST  # Enable command substitution in prompts

gcloud_prompt_info() {
  local config_path="$HOME/.config/gcloud/active_config"
  [[ -r "$config_path" ]] || return
  local config
  config=$(<"$config_path") || return
  config=${config//$'\n'/}
  [[ -n "$config" ]] && print " [%F{220}$config%f]"
}

# Fast custom prompt function to show truncated path with ellipsis
custom_pwd() {
  local pwd_length=3  # Number of directories to show
  local current_dir="$PWD"
  
  # Replace home directory with ~
  if [[ "$current_dir" == "$HOME"* ]]; then
    current_dir="~${current_dir#$HOME}"
  fi
  
  # Split path into array using zsh built-in
  local -a path_parts
  path_parts=(${(s:/:)current_dir})
  
  # If path is short enough, return as is
  if (( ${#path_parts} <= $((pwd_length + 1)) )); then
    echo "$current_dir"
  else
    # Take last pwd_length parts using zsh array slicing
    local short_path="${(j:/:)path_parts[-$pwd_length,-1]}"
    echo "~/.../$short_path"
  fi
}

case "$OSTYPE" in
  linux*)
    export PROMPT='%F{82}%n@%m%f:%F{63}$(custom_pwd)%f$(gcloud_prompt_info)$ '
    ;;
  *)
    export PROMPT='%F{141}%n@%m%f:%F{39}$(custom_pwd)%f$(gcloud_prompt_info)$ '
    ;;
esac

#
# Locale
#
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8
export LANGUAGE=en_US.UTF-8

#
# Alias
#
# some more ls aliases
alias ll='ls -alF'
alias la='ls -A'
alias l='ls -CF'

# docker alias
alias dc='docker compose'
alias dcps='docker compose ps'
alias dcud='docker compose up -d'
alias dcudb='docker compose up -d --build'
alias dcudf='docker compose up -d --force-recreate'
alias dce='docker compose exec $(docker compose ps --services)'
alias dcl='docker compose logs'
alias dcd='docker compose down'

# gcloud
alias gca='gcloud config configurations activate'
alias gcl='gcloud config configurations list'
alias gal='gcloud auth application-default login'
alias gcsp='gcloud config set project'

#
# Path

# cache directory
[[ -d "$HOME/.zsh/cache" ]] || mkdir -p "$HOME/.zsh/cache"

# iterm2
test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh" || true

# cuda
if [ -d "/usr/local/cuda/bin" ]; then
    export PATH="/usr/local/cuda/bin:$PATH"
    export LD_LIBRARY_PATH="/usr/local/cuda/lib64:$LD_LIBRARY_PATH"
fi

# dotenv
if type direnv > /dev/null 2>&1; then
    eval "$(direnv hook zsh)"
fi

# .local
export PATH="$HOME/.local/bin:$PATH"

# docker build
if [[ $(uname -m) == "arm64" ]]; then
  export DOCKER_DEFAULT_PLATFORM=linux/amd64
fi

# go
export GOPATH=$HOME/go
export PATH=$PATH:$GOPATH/bin

# volta
export VOLTA_HOME="$HOME/.volta"
export PATH="$VOLTA_HOME/bin:$PATH"

# rye
if [ -d "$HOME/.rye" ]; then
    source "$HOME/.rye/env"
fi

# mise
if [ -s ${HOME}/.local/bin/mise ]; then
  eval "$(${HOME}/.local/bin/mise activate zsh)"
fi
